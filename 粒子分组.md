可以将发射器 影响器进行分组
```
import QtQuick
import QtQuick.Controls
import QtQuick.Particles

Window{
    id:root2
    width:800
    height: 800
    visible:true

    Rectangle {
    id: root
    width: 480; height: 240
    color: "#1F1F1F"
    property bool tracer: false

    ParticleSystem {
    id: particleSystem
    }
    //喷火粒子
    ImageParticle {
    id: smokePainter
    system: particleSystem
    groups: ['smoke']
    source: "assets/particle.png"
    alpha: 1
    }

    //火箭粒子
    ImageParticle {
    id: rocketPainter
    system: particleSystem
    groups: ['rocket']
    source: "assets/rocket.png"
    entryEffect: ImageParticle.Fade
    }


    //烟火粒子
    ImageParticle {
    id: sparklePainter
    system: particleSystem
    groups: ['sparkle']
    color: 'red'
    colorVariation: 0.6
    source: "assets/star.png"
    alpha: 0.3
    }
    //火箭发射器
    Emitter {
    id: rocketEmitter
    anchors.bottom: root.bottom
    width: parent.width; height: 40
    system: particleSystem
    group: 'rocket'
    emitRate: 4
    maximumEmitted: 8  //交错脉冲 最大数
    lifeSpan: 4800
    lifeSpanVariation: 400
    size: 32
    velocity: AngleDirection { angle: 270; magnitude: 150; magnitudeVariation: 10 }
    acceleration: AngleDirection { angle: 90; magnitude: 50 }
    }

    //火箭喷火  轨迹发射器
    TrailEmitter {
    id: smokeEmitter
    system: particleSystem
    group: 'smoke'
    follow: 'rocket'
    size: 16
    sizeVariation: 8
    emitRatePerParticle: 16//发射速率
    velocity: AngleDirection { angle: 90; magnitude: 100; angleVariation: 15 }
    lifeSpan: 200
    }

    Friction {
    groups: ['rocket']
    anchors.top: parent.top
    width: parent.width; height: 80
    system: particleSystem
    threshold: 5
    factor: 0.9
    }
    Turbulence {
    groups: ['rocket']
    anchors.bottom: parent.bottom
    width: parent.width; height: 160
    system: particleSystem
    strength:25

    }



    GroupGoal {
    id: rocketChanger
    anchors.top: parent.top
    width: parent.width; height: 80
    system: particleSystem
    groups: ['rocket'] //哪些逻辑粒子组将受到影响。如果是空的，就会影响到所有的粒子。
    goalState: 'explosion'
    jump: true

    }

    //用于设置粒子组的属性  sparkle的效果
    ParticleGroup {
    name: 'explosion'
    system: particleSystem
    //TrailEmitters将自动跟随该组

    TrailEmitter {
    id: explosionEmitter
    anchors.fill: parent
    group: 'sparkle'
    follow: 'rocket'
    lifeSpan: 750
    emitRatePerParticle: 200
    size: 16
    velocity: AngleDirection { angle: -90; angleVariation: 180; magnitude: 50 }
    }

    TrailEmitter {
    id: explosion2Emitter
    anchors.fill: parent
    group: 'sparkle' //指定了它们属于sparkle组，以便在其他地方引用该组件。
    follow: 'rocket'
    lifeSpan: 250
    emitRatePerParticle: 100
    size: 16
    velocity: AngleDirection { angle: 90; angleVariation: 15; magnitude: 400 }
    }
    }
    }
}
```
